apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def storePw = hasProperty('RELEASE_STORE_PASSWORD') ? RELEASE_STORE_PASSWORD : System.getenv('RELEASE_STORE_PASSWORD')
def keyPw = hasProperty('RELEASE_KEY_PASSWORD') ? RELEASE_KEY_PASSWORD : System.getenv('RELEASE_KEY_PASSWORD')

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    signingConfigs {
        release {
            storeFile file('../pietsmiet_android.jks')
            storePassword storePw
            keyAlias 'l3d00mandroidkey'
            keyPassword keyPw
        }
    }
    defaultConfig {
        applicationId "de.pscom.pietsmiet"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 5
        versionName "0.4"

    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue "string", "app_name", "PietSmiet Community App"
        }
        debug {
            resValue "string", "app_name", "Pietsmiet " + gitBranch()
        }
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
repositories {
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'

    compile 'com.google.firebase:firebase-core:10.2.1'
    compile 'com.google.firebase:firebase-messaging:10.2.1'
    compile 'com.google.firebase:firebase-crash:10.2.1'

    compile 'io.reactivex:rxjava:1.2.9'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.2.0'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'

    compile 'com.mikepenz:aboutlibraries:5.9.5'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    testCompile 'org.robolectric:robolectric:3.3.2'
    testCompile "org.mockito:mockito-core:2.7.22"
    testCompile 'com.squareup.okhttp3:mockwebserver:3.6.0'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.json:json:20160810'
    testCompile 'commons-io:commons-io:2.5'
}
// http://stackoverflow.com/a/42095944/4026792
def gitBranch() {
    try {
        def branch = ""
        def proc = "git rev-parse --abbrev-ref HEAD".execute()
        proc.in.eachLine { line -> branch = line }
        proc.err.eachLine { line -> println line }
        proc.waitFor()
        branch
    } catch (ignored) {
        return ""
    }
}

apply plugin: 'com.google.gms.google-services'
