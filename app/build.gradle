apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def storePw = hasProperty('RELEASE_STORE_PASSWORD') ? RELEASE_STORE_PASSWORD : System.getenv('RELEASE_STORE_PASSWORD')
def keyPw = hasProperty('RELEASE_KEY_PASSWORD') ? RELEASE_KEY_PASSWORD : System.getenv('RELEASE_KEY_PASSWORD')

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    signingConfigs {
        release {
            storeFile file('../pietsmiet_android.jks')
            storePassword storePw
            keyAlias 'l3d00mandroidkey'
            keyPassword keyPw
        }
    }
    defaultConfig {
        applicationId "de.pscom.pietsmiet"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 3
        versionName "0.2"

    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue "string", "app_name", "PietSmiet Community App"
            resValue "string", "app_version", "Version: $defaultConfig.versionName"
        }
        debug {
            resValue "string", "app_name", "Pietsmiet " + gitBranch()
            resValue "string", "app_version", "Version: $defaultConfig.versionName - DEV"
            setProperty("archivesBaseName", "Pietsmiet-Community-App-$defaultConfig.versionName")
        }
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
repositories {
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'

    compile 'com.google.firebase:firebase-core:10.2.1'
    compile 'com.google.firebase:firebase-messaging:10.2.1'
    compile 'com.google.firebase:firebase-database:10.2.1'
    compile 'com.google.firebase:firebase-crash:10.2.1'

    compile 'org.facebook4j:facebook4j-core:2.4.8'
    compile 'org.twitter4j:twitter4j-core:4.0.6'

    compile 'io.reactivex:rxjava:1.2.9'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.2.0'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'

    compile 'com.github.ahorn:android-rss:599fdab'
    compile('com.mikepenz:aboutlibraries:5.9.5')

    testCompile 'junit:junit:4.12'
}
// http://stackoverflow.com/a/42095944/4026792
def gitBranch() {
    try {
        def branch = ""
        def proc = "git rev-parse --abbrev-ref HEAD".execute()
        proc.in.eachLine { line -> branch = line }
        proc.err.eachLine { line -> println line }
        proc.waitFor()
        branch
    } catch (ignored) {
        return ""
    }
}

apply plugin: 'com.google.gms.google-services'
