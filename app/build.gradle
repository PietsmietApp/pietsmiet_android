apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.google.firebase.firebase-perf'

def storePw = hasProperty('RELEASE_STORE_PASSWORD') ? RELEASE_STORE_PASSWORD : System.getenv('RELEASE_STORE_PASSWORD')
def keyPw = hasProperty('RELEASE_KEY_PASSWORD') ? RELEASE_KEY_PASSWORD : System.getenv('RELEASE_KEY_PASSWORD')

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    signingConfigs {
        release {
            storeFile file('../pietsmiet_android.jks')
            storePassword storePw
            keyAlias 'l3d00mandroidkey'
            keyPassword keyPw
        }
    }
    defaultConfig {
        applicationId "de.pscom.pietsmiet"
        minSdkVersion 17
        targetSdkVersion 26
        versionCode 16
        versionName "1.5"
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue "string", "app_name", "PietSmiet Community App"
        }
        debug {
            resValue "string", "app_name", "Pietsmiet " + gitBranch()
        }
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}
repositories {
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile 'com.android.support:appcompat-v7:26.0.0-alpha1'
    compile 'com.android.support:design:26.0.0-alpha1'
    compile 'com.android.support:cardview-v7:26.0.0-alpha1'
    compile 'com.android.support:recyclerview-v7:26.0.0-alpha1'
    compile 'com.android.support:customtabs:26.0.0-alpha1'

    compile 'com.google.firebase:firebase-core:11.0.4'
    compile 'com.google.firebase:firebase-messaging:11.0.4'
    compile 'com.google.firebase:firebase-crash:11.0.4'
    compile 'com.google.firebase:firebase-config:11.0.4'
    compile 'com.google.firebase:firebase-perf:11.0.4'

    compile 'io.reactivex:rxjava:1.3.0'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'

    compile 'com.jakewharton:butterknife:8.7.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'

    compile 'com.mikepenz:aboutlibraries:5.9.6'

    compile 'com.github.bumptech.glide:glide:4.0.0-RC1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.0.0-RC1'
    compile ("com.github.bumptech.glide:recyclerview-integration:4.0.0-RC1") {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }

    debugCompile 'com.facebook.stetho:stetho:1.5.0'
    debugCompile 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'

    testCompile 'org.robolectric:robolectric:3.4'
    testCompile 'org.mockito:mockito-core:2.8.47'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.8.0'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.4-atlassian-1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.json:json:20170516'
    testCompile 'commons-io:commons-io:2.5'
}
// http://stackoverflow.com/a/42095944/4026792
def gitBranch() {
    try {
        def branch = ""
        def proc = "git rev-parse --abbrev-ref HEAD".execute()
        proc.in.eachLine { line -> branch = line }
        proc.err.eachLine { line -> println line }
        proc.waitFor()
        branch
    } catch (ignored) {
        return ""
    }
}

apply plugin: 'com.google.gms.google-services'
